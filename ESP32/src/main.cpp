#include <Arduino.h>
//https://docs.google.com/document/d/1Vxh6Z6RBmllpE-TCUfmOebgBQnEqvF-Deb4upEB9mnA/edit?tab=t.0

// Motor pins
#define Limit_X 18
#define Limit_Z 4
#define Relay_Pump 23
const int A_IN1 = 14, A_IN2 = 12, A_IN3 = 13, A_IN4 = 15; // X
const int B_IN1 = 27, B_IN2 = 26, B_IN3 = 25, B_IN4 = 33; // Z

// Step sequence
const int STEP_SEQ[8][4] = {
    {1, 0, 0, 0}, {1, 1, 0, 0}, {0, 1, 0, 0}, {0, 1, 1, 0},
     {0, 0, 1, 0}, {0, 0, 1, 1}, {0, 0, 0, 1}, {1, 0, 0, 1}};

const int STEPS_PER_REV = 4096; // ‡∏™‡∏£‡πâ‡∏≤‡∏á‡∏ï‡∏±‡∏ß‡πÅ‡∏õ‡∏£‡∏Ñ‡∏ß‡∏≤‡∏°‡∏•‡∏∞‡πÄ‡∏≠‡∏µ‡∏¢‡∏î‡∏Å‡∏≤‡∏£‡πÄ‡∏Ñ‡∏•‡∏∑‡πà‡∏≠‡∏ô‡∏ó‡∏µ‡πà‡∏Ç‡∏≠‡∏á‡∏°‡∏≠‡πÄ‡∏ï‡∏≠‡∏£‡πå
const int STEP_DELAY = 800; // ‡∏™‡∏£‡πâ‡∏≤‡∏á‡∏ï‡∏±‡∏ß‡πÅ‡∏õ‡∏£‡∏£‡∏∞‡∏¢‡∏∞‡πÄ‡∏ß‡∏•‡∏≤‡∏´‡∏ô‡πà‡∏ß‡∏á‡∏Ç‡∏≠‡∏á‡∏Å‡∏≤‡∏£‡∏ó‡∏≥‡∏á‡∏≤‡∏ô‡πÅ‡∏ï‡πà‡∏•‡∏∞‡∏£‡∏≠‡∏ö
const float DIST_PER_REV = 15.71; // ‡∏Ñ‡∏ß‡∏≤‡∏°‡∏¢‡∏≤‡∏ß‡∏£‡∏≠‡∏ö‡πÄ‡∏û‡∏•‡∏≤
const float HOME_OFFSET_MM = 3;  // ‡∏Å‡∏≥‡∏´‡∏ô‡∏î‡∏£‡∏∞‡∏¢‡∏∞‡∏ó‡∏µ‡πà‡∏ï‡πâ‡∏≠‡∏á‡∏Å‡∏≤‡∏£‡πÉ‡∏´‡πâ‡∏ñ‡∏≠‡∏¢‡∏≠‡∏≠‡∏Å‡∏°‡∏≤‡∏´‡∏•‡∏±‡∏á‡∏à‡∏≤‡∏Å‡∏ä‡∏ô‡∏Å‡∏±‡∏ö limit switch
// Motor state
float stepsToMoveX = 0, stepsToMoveZ = 0; // ‡∏à‡∏≥‡∏ô‡∏ß‡∏ô‡∏´‡∏ô‡πà‡∏ß‡∏¢‡πÄ‡∏Ñ‡∏•‡∏∑‡πà‡∏≠‡∏ô‡∏ó‡∏µ‡πà‡∏Ç‡∏≠‡∏á‡∏°‡∏≠‡πÄ‡∏ï‡∏≠‡∏£‡πå‡∏ó‡∏µ‡πà‡∏à‡∏∞‡∏ï‡πâ‡∏≠‡∏á‡∏Ç‡∏¢‡∏±‡∏ö‡πÑ‡∏õ
int dirX = 1, dirZ = 1; // ‡∏ó‡∏¥‡∏®‡∏ó‡∏≤‡∏á‡πÄ‡∏£‡∏¥‡πà‡∏°‡∏ï‡πâ‡∏ô‡∏Ç‡∏≠‡∏á‡∏°‡∏≠‡πÄ‡∏ï‡∏≠‡∏£‡πå 1 = MotorZ = ‡∏ã‡πâ‡∏≤‡∏¢/MotorX = ‡∏ö‡∏ô
int seqIndexX = 0, seqIndexZ = 0; // ‡∏•‡∏≥‡∏î‡∏±‡∏ö‡∏Å‡∏≤‡∏£‡∏ó‡∏≥‡∏á‡∏≤‡∏ô‡∏Ç‡∏≠‡∏á motor ‡∏ó‡∏µ‡πà‡∏ï‡πâ‡∏≠‡∏á‡∏ó‡∏≥‡∏á‡∏≤‡∏ô‡πÉ‡∏ô‡∏Ç‡∏ì‡∏∞‡∏ô‡∏±‡πâ‡∏ô
unsigned long lastStepTimeX = 0, lastStepTimeZ = 0;// ‡πÄ‡∏Å‡πá‡∏ö‡πÄ‡∏ß‡∏•‡∏≤‡∏•‡πà‡∏≤‡∏™‡∏∏‡∏î‡∏ó‡∏µ‡πà‡∏ó‡∏≥‡∏á‡∏≤‡∏ô‡πÑ‡∏õ
float currentPosX = 0, currentPosZ = 0;// ‡∏´‡∏ô‡πà‡∏ß‡∏¢‡∏Å‡∏≤‡∏£‡πÄ‡∏Ñ‡∏•‡∏∑‡πà‡∏≠‡∏ô‡∏ó‡∏µ‡πà‡∏•‡πà‡∏≤‡∏™‡∏∏‡∏î‡∏ó‡∏µ‡πà‡πÄ‡∏Ñ‡∏•‡∏∑‡πà‡∏≠‡∏ô‡∏ó‡∏µ‡πà‡πÑ‡∏î‡πâ

// ‡∏à‡∏≥‡∏ô‡∏ß‡∏ô‡∏´‡∏ô‡πà‡∏ß‡∏¢‡πÄ‡∏Ñ‡∏•‡∏∑‡πà‡∏≠‡∏ô‡∏ó‡∏µ‡πà‡∏ñ‡∏≠‡∏¢‡∏´‡∏•‡∏±‡∏á
long backoffSteps = (HOME_OFFSET_MM / DIST_PER_REV) * STEPS_PER_REV;

// ‡∏™‡∏π‡∏ï‡∏£‡∏°‡∏≤‡∏à‡∏≤‡∏Å ‡∏à‡∏≥‡∏ô‡∏ß‡∏ô‡∏£‡∏≠‡∏ö‡∏ó‡∏µ‡πà‡∏ï‡πâ‡∏≠‡∏á‡∏´‡∏°‡∏∏‡∏ô‡πÉ‡∏ô‡∏£‡∏∞‡∏¢‡∏∞‡∏ñ‡∏≠‡∏¢‡∏´‡∏•‡∏±‡∏á * ‡∏Ñ‡πà‡∏≤‡∏Ñ‡∏ß‡∏≤‡∏°‡∏•‡∏∞‡πÄ‡∏≠‡∏µ‡∏¢‡∏î‡∏Ç‡∏≠‡∏á‡∏Å‡∏≤‡∏£‡∏ó‡∏≥‡∏á‡∏≤‡∏ô‡∏°‡∏≠‡πÄ‡∏ï‡∏≠‡∏£‡πå
// ‡∏à‡∏≥‡∏ô‡∏ß‡∏ô‡∏£‡∏≠‡∏ö‡∏ó‡∏µ‡πà‡∏ï‡πâ‡∏≠‡∏á‡∏´‡∏°‡∏∏‡∏ô‡πÉ‡∏ô‡∏£‡∏∞‡∏¢‡∏∞‡∏ñ‡∏≠‡∏¢‡∏´‡∏•‡∏±‡∏á = ‡∏£‡∏∞‡∏¢‡∏∞‡∏ó‡∏µ‡πà‡∏à‡∏∞‡∏ñ‡∏≠‡∏¢ / ‡∏Ñ‡∏ß‡∏≤‡∏°‡∏¢‡∏≤‡∏ß‡∏£‡∏≠‡∏ö‡πÄ‡∏û‡∏•‡∏≤ = 3 / 15.71
// ‡∏Ñ‡πà‡∏≤‡∏Ñ‡∏ß‡∏≤‡∏°‡∏•‡∏∞‡πÄ‡∏≠‡∏µ‡∏¢‡∏î‡∏Ç‡∏≠‡∏á‡∏Å‡∏≤‡∏£‡∏ó‡∏≥‡∏á‡∏≤‡∏ô‡∏°‡∏≠‡πÄ‡∏ï‡∏≠‡∏£‡πå = 4096

// ‡πÄ‡∏õ‡πá‡∏ô‡∏ï‡∏±‡∏ß‡πÄ‡∏ä‡πá‡∏Ñ‡∏ß‡πà‡∏≤ limit switch ‡∏ó‡∏≥‡∏á‡∏≤‡∏ô‡∏£‡∏∂‡∏¢‡∏±‡∏á
bool flagX, flagZ;

// targetX/Z ‡∏Ñ‡∏∑‡∏≠ ‡∏û‡∏¥‡∏Å‡∏±‡∏î‡∏ó‡∏µ‡πà‡∏à‡∏∞‡πÄ‡∏Ñ‡∏•‡∏∑‡πà‡∏≠‡∏ô‡∏ó‡∏µ‡πà‡πÑ‡∏õ diffX/Z ‡∏Ñ‡πà‡∏≤‡∏û‡∏¥‡∏Å‡∏±‡∏î‡πÄ‡∏õ‡πâ‡∏≤‡∏´‡∏°‡∏≤‡∏¢ - ‡∏û‡∏¥‡∏Å‡∏±‡∏î‡∏õ‡∏±‡∏à‡∏à‡∏∏‡∏ö‡∏±‡∏ô
// ‡πÄ‡∏û‡∏∑‡πà‡∏≠‡∏£‡∏π‡πâ‡πÑ‡∏î‡πâ‡∏ß‡πà‡∏≤‡∏ï‡πâ‡∏≠‡∏á‡πÄ‡∏Ñ‡∏•‡∏∑‡πà‡∏≠‡∏ô‡∏ó‡∏µ‡πà‡∏´‡πà‡∏≤‡∏á‡∏à‡∏≤‡∏Å‡∏à‡∏∏‡∏î‡∏õ‡∏±‡∏à‡∏à‡∏∏‡∏ö‡∏±‡∏ô‡πÄ‡∏ó‡πà‡∏≤‡πÑ‡∏´‡∏£‡πà
float targetX, diffX, targetZ, diffZ;

‡πà// ‡πÄ‡∏õ‡πá‡∏ô‡∏ï‡∏±‡∏ß‡πÄ‡∏ä‡πá‡∏Ñ‡∏ß‡πà‡∏≤‡∏ï‡∏≠‡∏ô‡∏ô‡∏µ‡πâ‡∏Å‡∏≥‡∏•‡∏±‡∏á‡∏ó‡∏≥‡∏ü‡∏±‡∏á‡∏Å‡πå‡∏ä‡∏±‡∏ô doHoming ‡∏≠‡∏¢‡∏π‡πà‡∏£‡∏∂‡∏õ‡πà‡∏≤‡∏ß
bool isdohoming = false;

// ‡∏£‡∏∞‡∏¢‡∏∞‡πÄ‡∏£‡∏¥‡πà‡∏°‡∏ï‡πâ‡∏ô‡πÅ‡∏•‡∏∞‡∏£‡∏∞‡∏¢‡∏∞‡∏™‡∏∏‡∏î‡∏ó‡πâ‡∏≤‡∏¢‡∏Ç‡∏≠‡∏á‡πÅ‡∏Å‡∏ô X
float X_MIN_POS = 300.0, X_MAX_POS = 900.0; 
float Z_CEN_POS = 670.0; // ‡∏£‡∏∞‡∏¢‡∏∞‡∏Å‡∏∂‡πà‡∏á‡∏Å‡∏•‡∏≤‡∏á‡∏Ç‡∏≠‡∏á‡πÅ‡∏Å‡∏ô Z

// ================= Interrupt =================
void IRAM_ATTR LimitInteruptX()
{
  flagX = true;

  dirX = (currentPosX > 18413) ? -1 : 1;
}
void IRAM_ATTR LimitInteruptZ()
{
  flagZ = true;

  dirZ = (currentPosZ > 9843) ? -1 : 1;
}

// ================= Step Motor =================
void setOutputs(int in1, int in2, int in3, int in4, const int pattern[4])
{
  digitalWrite(in1, pattern[0]);
  digitalWrite(in2, pattern[1]);
  digitalWrite(in3, pattern[2]);
  digitalWrite(in4, pattern[3]);
}
void stopMotor(int in1, int in2, int in3, int in4)
{
  digitalWrite(in1, 0);
  digitalWrite(in2, 0);
  digitalWrite(in3, 0);
  digitalWrite(in4, 0);
}

void stepMotorX()
{
  if (micros() - lastStepTimeX >= STEP_DELAY)
  {
    seqIndexX = (seqIndexX + dirX + 8) % 8;
    setOutputs(A_IN1, A_IN2, A_IN3, A_IN4, STEP_SEQ[seqIndexX]);
    lastStepTimeX = micros();
    currentPosX += dirX;
    // printf("CurrentPosX : %d\n", currentPosX);
    stepsToMoveX--;
  }
}
void stepMotorZ()
{
  if (micros() - lastStepTimeZ >= STEP_DELAY)
  {
    seqIndexZ = (seqIndexZ + dirZ + 8) % 8;
    setOutputs(B_IN1, B_IN2, B_IN3, B_IN4, STEP_SEQ[seqIndexZ]);
    lastStepTimeZ = micros();
    currentPosZ += dirZ;
    // printf("CurrentPosZ : %d\n", currentPosZ);
    stepsToMoveZ--;
  }
}

// ================= Move To =================
void moveX_to(float distance_pixel)
{
  targetX = distance_pixel * 40.92;
  diffX = targetX - currentPosX;
  dirX = (diffX >= 0) ? 1 : -1;
  stepsToMoveX = abs(diffX);
}
void moveZ_to(float distance_pixel)
{
  targetZ = distance_pixel * 29.38;
  diffZ = targetZ - currentPosZ;
  dirZ = (diffZ >= 0) ? 1 : -1;
  stepsToMoveZ = abs(diffZ);
  // printf("targetZ : %.2f , currentZ : %.2f , diffZ : diffZ : %.2f , stepstomoveZ : %.2f\n",targetZ,currentPosZ,diffZ,stepsToMoveZ);
}

void backoffX()
{
  detachInterrupt(digitalPinToInterrupt(Limit_X));
  flagX = false;
  for (long i = 0; i < backoffSteps; i++)
  {
    stepMotorX();
    delayMicroseconds(STEP_DELAY);
  }
  stopMotor(A_IN1, A_IN2, A_IN3, A_IN4);
  attachInterrupt(digitalPinToInterrupt(Limit_X), LimitInteruptX, FALLING);
  stepsToMoveX = 0;
  // currentPosX = 0;
}

void backoffZ()
{
  detachInterrupt(digitalPinToInterrupt(Limit_Z));
  flagZ = false;
  for (long i = 0; i < backoffSteps; i++)
  {
    stepMotorZ();
    delayMicroseconds(STEP_DELAY);
  }
  stopMotor(B_IN1, B_IN2, B_IN3, B_IN4);
  attachInterrupt(digitalPinToInterrupt(Limit_Z), LimitInteruptZ, FALLING);
  stepsToMoveZ = 0;
  // currentPosZ = 0;
}

// ================= Homing Function =================
void doHoming()
{
  isdohoming = true;
  // ---------- Homing X (Blocking, Silent) ----------
  dirX = -1;
  while (!flagX)
  {
    stepMotorX();
  } // ‚¨ÖÔ∏è Blocking (‡∏ï‡πâ‡∏≠‡∏á‡∏û‡∏∂‡πà‡∏á‡∏û‡∏≤ stepMotorX Non-Blocking)
  backoffX();
  flagX = false;
  currentPosX = 0;

  // ---------- Homing Z (Blocking, Silent) ----------
  dirZ = -1;
  while (!flagZ)
  {
    stepMotorZ();
  }
  backoffZ();
  flagZ = false;
  currentPosZ = 0;

  isdohoming = false;
}

// üéØ SCAN STATE MACHINE VARIABLES
enum State
{
  // ===================  Default  =====================
  IDLE,
  HOMING,

  // ===================  Scan  =====================
  MOVING_SCAN_Z,
  MOVING_SCAN_X,
  HOMING_SCAN,
  WAITING_ACK,
  SCANNING_X_MOVE,
  WAITING_ACK_X,
  SCAN_COMPLETE,

  // ===================  Watering  =====================
  MOVING_WATER_Z,
  MOVING_WATER_X,
  WATER_COMPLETE
};

State currentState = IDLE; // ‡πÄ‡∏õ‡πá‡∏ô‡∏Å‡∏≤‡∏£‡∏Å‡∏≥‡∏´‡∏ô‡∏î ‡∏™‡∏ñ‡∏≤‡∏ô‡∏∞ ‡πÄ‡∏£‡∏¥‡πà‡∏°‡∏ï‡πâ‡∏ô
int currentShot = 0; // ‡∏à‡∏≥‡∏ô‡∏ß‡∏ô‡∏ó‡∏µ‡πà‡πÑ‡∏î‡πâ‡∏ñ‡πà‡∏≤‡∏¢‡πÑ‡∏õ‡∏•‡πà‡∏≤‡∏™‡∏∏‡∏î
int totalShots = 0; // ‡∏à‡∏≥‡∏ô‡∏ß‡∏ô‡∏ó‡∏µ‡πà‡∏ï‡πâ‡∏≠‡∏á‡∏ñ‡πà‡∏≤‡∏¢
float currentGap = 0.0; // ‡∏Ñ‡πà‡∏≤‡∏Ñ‡∏ß‡∏≤‡∏°‡∏´‡πà‡∏≤‡∏á‡∏ä‡∏î‡πÄ‡∏ä‡∏¢‡∏´‡∏•‡∏±‡∏á‡∏à‡∏≤‡∏Å‡∏ó‡∏µ‡πà‡∏£‡∏ß‡∏°‡∏£‡∏π‡∏õ

int id; // ‡πÄ‡∏Å‡πá‡∏ö id ‡∏ó‡∏µ‡πà‡∏™‡πà‡∏á‡∏°‡∏≤
String incomingCommand = ""; // ‡πÄ‡∏Å‡πá‡∏ö ‡∏Ñ‡∏≥‡∏™‡∏±‡πà‡∏á‡∏ó‡∏±‡πâ‡∏á‡∏´‡∏°‡∏î ‡∏ó‡∏µ‡πà‡∏™‡πà‡∏á‡∏°‡∏≤
String currentCommand = ""; // ‡πÄ‡∏Å‡πá‡∏ö ‡∏Ñ‡∏≥‡∏™‡∏±‡πà‡∏á‡∏ó‡∏µ‡πà‡∏ó‡∏≥‡∏Å‡∏≤‡∏£‡πÅ‡∏¢‡∏Å‡∏≠‡∏≠‡∏Å‡∏°‡∏≤‡∏à‡∏≤‡∏Å incomingCommand 

// Relay
unsigned long startTime = 0; // ‡πÄ‡∏ß‡∏•‡∏≤‡πÄ‡∏£‡∏¥‡πà‡∏°‡∏ï‡πâ‡∏ô
const long duration = 3000; // 3000 ‡∏°‡∏¥‡∏•‡∏•‡∏¥‡∏ß‡∏¥‡∏ô‡∏≤‡∏ó‡∏µ = 3 ‡∏ß‡∏¥‡∏ô‡∏≤‡∏ó‡∏µ
bool pump_active = false;   // ‡∏™‡∏ñ‡∏≤‡∏ô‡∏∞‡πÄ‡∏£‡∏¥‡πà‡∏°‡∏ï‡πâ‡∏ô

// ----------------------------------------------------
// üéØ NON-BLOCKING SCAN/MOVE LOGIC
// ----------------------------------------------------

void handleScan()
{
  switch (currentState)
  {
  case IDLE:
    break;

  case HOMING:
    doHoming();
    currentState = IDLE;
    break;

  case HOMING_SCAN:
    // 1. ‡∏ó‡∏≥ Homing ‡πÅ‡∏ö‡∏ö Blocking
    doHoming();
    moveZ_to(Z_CEN_POS / 2);
    currentState = MOVING_SCAN_Z; // ‡πÑ‡∏õ‡∏™‡∏ñ‡∏≤‡∏ô‡∏∞‡∏ñ‡∏±‡∏î‡πÑ‡∏õ
    break;

  case MOVING_SCAN_Z:
    // 2. ‡∏Ç‡∏¢‡∏±‡∏ö Z ‡πÑ‡∏õ‡∏ï‡∏£‡∏á‡∏Å‡∏•‡∏≤‡∏á (Non-Blocking Move Init)
    // üí° Logic: ‡∏ñ‡πâ‡∏≤ stepsToMoveZ ‡∏¢‡∏±‡∏á > 0 ‡πÉ‡∏´‡πâ stepMotorZ ‡πÉ‡∏ô loop() ‡∏à‡∏±‡∏î‡∏Å‡∏≤‡∏£‡∏ï‡πà‡∏≠
    if (stepsToMoveZ <= 0)
    {
      // ‡∏ñ‡πâ‡∏≤‡∏Ç‡∏¢‡∏±‡∏ö‡πÄ‡∏™‡∏£‡πá‡∏à‡πÅ‡∏•‡πâ‡∏ß (stepMotorZ ‡∏•‡∏î stepsToMoveZ ‡∏à‡∏ô‡∏´‡∏°‡∏î)
      stopMotor(B_IN1, B_IN2, B_IN3, B_IN4);
      Serial.println("WAITING_COMMAND"); // ‚¨ÖÔ∏è ‡∏™‡∏±‡πà‡∏á‡πÉ‡∏´‡πâ Python ‡∏™‡πà‡∏á CAPTURE:shots
      Serial.flush();
      currentState = WAITING_ACK;
    }
    break;

  case WAITING_ACK:
    // 3. ‡∏£‡∏≠‡∏Ñ‡∏≥‡∏™‡∏±‡πà‡∏á CAPTURE:shots ‡∏à‡∏≤‡∏Å Python ‡πÉ‡∏ô processCommand()
    break;

  case SCANNING_X_MOVE:
    // 4. ‡∏Ç‡∏¢‡∏±‡∏ö X ‡πÑ‡∏õ‡∏à‡∏∏‡∏î‡∏ñ‡∏±‡∏î‡πÑ‡∏õ (Non-Blocking Move Init)
    Serial.println(currentShot);
    if (currentShot < totalShots)
    {
      float spotX = min(X_MIN_POS + (currentGap * currentShot), X_MAX_POS - 50);
      moveX_to(spotX);
      currentState = MOVING_SCAN_X;
    }
    else
    {
      // 5. ‡∏™‡πÅ‡∏Å‡∏ô‡∏Ñ‡∏£‡∏ö‡πÅ‡∏•‡πâ‡∏ß
      currentState = SCAN_COMPLETE;
    }
    break;

  case MOVING_SCAN_X:
    // 6. ‡∏£‡∏≠‡πÉ‡∏´‡πâ stepsToMoveX ‡πÄ‡∏™‡∏£‡πá‡∏à
    if (stepsToMoveX <= 0)
    {
      // 7. ‡∏ñ‡∏∂‡∏á‡∏à‡∏∏‡∏î‡πÅ‡∏•‡πâ‡∏ß: ‡∏™‡πà‡∏á‡∏™‡∏±‡∏ç‡∏ç‡∏≤‡∏ì ARRIVED ‡πÑ‡∏õ Python
      stopMotor(A_IN1, A_IN2, A_IN3, A_IN4);
      Serial.println("ARRIVED");
      Serial.flush();
      currentState = WAITING_ACK_X; // ‡πÄ‡∏Ç‡πâ‡∏≤‡∏™‡∏π‡πà‡∏™‡∏ñ‡∏≤‡∏ô‡∏∞‡∏£‡∏≠ CAPTURED
    }
    break;

  case WAITING_ACK_X:
    // 8. ‡∏£‡∏≠‡∏Å‡∏≤‡∏£‡∏ï‡∏≠‡∏ö‡∏£‡∏±‡∏ö 'CAPTURED' ‡∏à‡∏≤‡∏Å Python ‡πÉ‡∏ô processCommand()
    break;

  case SCAN_COMPLETE:
    // stopMotor(A_IN1, A_IN2, A_IN3, A_IN4);
    doHoming();                   // ‡∏Å‡∏•‡∏±‡∏ö‡πÑ‡∏õ Home ‡πÄ‡∏°‡∏∑‡πà‡∏≠‡πÄ‡∏™‡∏£‡πá‡∏à‡∏™‡∏¥‡πâ‡∏ô
    Serial.println("REPORT_END"); // ‚¨ÖÔ∏è ‡∏™‡πà‡∏á‡∏™‡∏±‡∏ç‡∏ç‡∏≤‡∏ì‡∏à‡∏ö‡∏á‡∏≤‡∏ô
    Serial.flush();
    currentState = IDLE;
    break;

  // ------------------------ WATERING -----------------------------------
  case MOVING_WATER_Z:
    if (stepsToMoveZ <= 0)
    {
      // ‡∏ñ‡πâ‡∏≤‡∏Ç‡∏¢‡∏±‡∏ö‡πÄ‡∏™‡∏£‡πá‡∏à‡πÅ‡∏•‡πâ‡∏ß (stepMotorZ ‡∏•‡∏î stepsToMoveZ ‡∏à‡∏ô‡∏´‡∏°‡∏î)
      stopMotor(B_IN1, B_IN2, B_IN3, B_IN4);
      // Serial.println("WAITING_COMMAND"); // ‚¨ÖÔ∏è ‡∏™‡∏±‡πà‡∏á‡πÉ‡∏´‡πâ Python ‡∏™‡πà‡∏á CAPTURE:shots
      // Serial.flush();
      currentState = MOVING_WATER_X;
    }
    break;

  case MOVING_WATER_X:
    if (stepsToMoveX <= 0)
    {
      stopMotor(A_IN1, A_IN2, A_IN3, A_IN4);
      // Serial.println("WAITING_COMMAND"); // ‚¨ÖÔ∏è ‡∏™‡∏±‡πà‡∏á‡πÉ‡∏´‡πâ Python ‡∏™‡πà‡∏á CAPTURE:shots
      // Serial.flush();
      currentState = WATER_COMPLETE;
    }
    break;

  case WATER_COMPLETE:
    if (!pump_active)
    {
      // ‚úÖ FIX 1: ‡∏¢‡∏Å‡πÄ‡∏•‡∏¥‡∏Å Interrupt ‡∏Å‡πà‡∏≠‡∏ô‡πÄ‡∏õ‡∏¥‡∏î‡∏£‡∏µ‡πÄ‡∏•‡∏¢‡πå
      detachInterrupt(digitalPinToInterrupt(Limit_X));
      detachInterrupt(digitalPinToInterrupt(Limit_Z));
      startTime = millis();           // ‡∏ö‡∏±‡∏ô‡∏ó‡∏∂‡∏Å‡πÄ‡∏ß‡∏•‡∏≤‡∏ó‡∏µ‡πà‡πÄ‡∏£‡∏¥‡πà‡∏°‡∏ó‡∏≥‡∏á‡∏≤‡∏ô
      digitalWrite(Relay_Pump, LOW); // ‡πÄ‡∏õ‡∏¥‡∏î‡∏£‡∏µ‡πÄ‡∏•‡∏¢‡πå
      pump_active = true;             // ‡∏ï‡∏±‡πâ‡∏á‡∏Ñ‡πà‡∏≤‡∏™‡∏ñ‡∏≤‡∏ô‡∏∞‡∏ß‡πà‡∏≤‡∏õ‡∏±‡πä‡∏°‡∏Å‡∏≥‡∏•‡∏±‡∏á‡∏ó‡∏≥‡∏á‡∏≤‡∏ô
    }

    // ‡∏ï‡∏£‡∏ß‡∏à‡∏™‡∏≠‡∏ö‡∏ß‡πà‡∏≤‡∏ñ‡∏∂‡∏á‡πÄ‡∏ß‡∏•‡∏≤‡∏õ‡∏¥‡∏î‡∏´‡∏£‡∏∑‡∏≠‡∏¢‡∏±‡∏á
    if (pump_active && (millis() - startTime >= duration))
    {
      digitalWrite(Relay_Pump, HIGH); // ‡∏õ‡∏¥‡∏î‡∏£‡∏µ‡πÄ‡∏•‡∏¢‡πå
      // ‚úÖ FIX 2: ‡πÄ‡∏õ‡∏¥‡∏î‡πÉ‡∏ä‡πâ‡∏á‡∏≤‡∏ô Interrupt ‡∏≠‡∏µ‡∏Å‡∏Ñ‡∏£‡∏±‡πâ‡∏á‡∏´‡∏•‡∏±‡∏á‡∏à‡∏≤‡∏Å‡∏õ‡∏¥‡∏î‡∏£‡∏µ‡πÄ‡∏•‡∏¢‡πå
      pump_active = false; // ‡∏ï‡∏±‡πâ‡∏á‡∏Ñ‡πà‡∏≤‡∏™‡∏ñ‡∏≤‡∏ô‡∏∞‡∏ß‡πà‡∏≤‡∏õ‡∏±‡πä‡∏°‡∏´‡∏¢‡∏∏‡∏î‡πÅ‡∏•‡πâ‡∏ß
      if (currentCommand == "WATER_SPECIFIC")
      {
        Serial.println("WATERING_SPECIFIC_COMPLETE");
        Serial.flush();
      }
      if (currentCommand == "WATER_ALL")
      {
        Serial.print("\nWATERING_"); // ‡∏™‡πà‡∏á‡∏Å‡∏≤‡∏£‡∏ï‡∏≠‡∏ö‡∏Å‡∏•‡∏±‡∏ö
        Serial.print(id);            // ‡∏™‡πà‡∏á‡∏Å‡∏≤‡∏£‡∏ï‡∏≠‡∏ö‡∏Å‡∏•‡∏±‡∏ö
        Serial.println("_COMPLETE"); // ‡∏™‡πà‡∏á‡∏Å‡∏≤‡∏£‡∏ï‡∏≠‡∏ö‡∏Å‡∏•‡∏±‡∏ö
        Serial.flush();
      }
      currentState = IDLE;
      attachInterrupt(digitalPinToInterrupt(Limit_X), LimitInteruptX, FALLING);
      attachInterrupt(digitalPinToInterrupt(Limit_Z), LimitInteruptZ, FALLING);
    }
    break;
  }
}

// ================= Setup =================
void setup()
{
  Serial.begin(115200);

  pinMode(Limit_X, INPUT_PULLUP);
  pinMode(Limit_Z, INPUT_PULLUP);
  pinMode(Relay_Pump, OUTPUT);

  attachInterrupt(digitalPinToInterrupt(Limit_X), LimitInteruptX, FALLING);
  attachInterrupt(digitalPinToInterrupt(Limit_Z), LimitInteruptZ, FALLING);
  pinMode(A_IN1, OUTPUT);
  pinMode(A_IN2, OUTPUT);
  pinMode(A_IN3, OUTPUT);
  pinMode(A_IN4, OUTPUT);
  pinMode(B_IN1, OUTPUT);
  pinMode(B_IN2, OUTPUT);
  pinMode(B_IN3, OUTPUT);
  pinMode(B_IN4, OUTPUT);
  digitalWrite(Relay_Pump, HIGH);
  // homing = false;
  Serial.println("Ready.");
}

void processCommand(String command);

void loop()
{
  // 1. Interrupt Logic (‡∏Ñ‡∏á‡πÄ‡∏î‡∏¥‡∏°)
  if (flagX)
  {
    backoffX();
  }
  if (flagZ)
  {
    backoffZ();
  }

  // 2. Motor Logic (Non-Blocking Stepper)
  if (stepsToMoveX > 0)
  {
    stepMotorX();
  }
  if (stepsToMoveZ > 0)
  {
    stepMotorZ();
  }

  // 3. Handle Scan State Machine
  handleScan();

  // 4. Serial Command Receiver
  while (Serial.available())
  {
    char incomingChar = Serial.read();
    if (incomingChar == '\n')
    {
      processCommand(incomingCommand);
      incomingCommand = "";
    }
    else
    {
      incomingCommand += incomingChar;
    }
  }
}

void processCommand(String command)
{
  command.toUpperCase(); // ‚¨ÖÔ∏è FIX: ‡πÉ‡∏ä‡πâ ToUpperCase() ‡πÄ‡∏û‡∏∑‡πà‡∏≠‡∏Ñ‡∏ß‡∏≤‡∏°‡πÄ‡∏Ç‡πâ‡∏≤‡∏Å‡∏±‡∏ô‡πÑ‡∏î‡πâ‡∏Å‡∏±‡∏ö Python

  if (command == "HOMING")
  {
    Serial.println("");
    Serial.flush();
    currentState = HOMING;
    return;
  }
  if (command == "SCAN")
  {
    // üéØ FIX: ‡πÄ‡∏£‡∏¥‡πà‡∏°‡∏ï‡πâ‡∏ô State Machine (‡∏à‡∏∞‡πÄ‡∏£‡∏µ‡∏¢‡∏Å Homing ‡πÉ‡∏ô handleScan)
    if (currentState == IDLE || currentState == SCAN_COMPLETE)
    {
      currentState = HOMING_SCAN;
      Serial.println("SCAN_ACK");
      Serial.flush();
    }
    else
    {
      Serial.println("ERROR: ALREADY_BUSY");
      Serial.flush();
    }
    return;
  }

  // üéØ ‡∏à‡∏±‡∏î‡∏Å‡∏≤‡∏£ CAPTURE:shots (‡∏£‡∏±‡∏ö‡∏Ñ‡πà‡∏≤‡∏ï‡∏±‡∏ß‡πÅ‡∏õ‡∏£)
  if (command.startsWith("CAPTURE:"))
  {
    if (currentState == WAITING_ACK)
    {
      int separatorIndex = command.indexOf(':');
      String valueStr = command.substring(separatorIndex + 1);
      totalShots = valueStr.toInt();

      currentGap = (X_MAX_POS - X_MIN_POS) / totalShots;
      currentShot = 0;

      // 2. ‡πÄ‡∏£‡∏¥‡πà‡∏°‡∏ï‡πâ‡∏ô‡∏•‡∏π‡∏õ‡∏™‡πÅ‡∏Å‡∏ô X
      currentState = SCANNING_X_MOVE;
      Serial.println("STATUS: X_SEQUENCE_START");
      return;
    }
  }

  // üéØ ‡∏à‡∏±‡∏î‡∏Å‡∏≤‡∏£‡∏™‡∏±‡∏ç‡∏ç‡∏≤‡∏ì ACK (CAPTURED) ‡∏à‡∏≤‡∏Å Python
  if (command == "CAPTURED" && currentState == WAITING_ACK_X)
  {
    currentShot++;                  // ‡πÑ‡∏õ‡∏à‡∏∏‡∏î‡∏ï‡πà‡∏≠‡πÑ‡∏õ
    currentState = SCANNING_X_MOVE; // ‡∏Å‡∏•‡∏±‡∏ö‡πÑ‡∏õ‡∏•‡∏π‡∏õ‡∏™‡πÅ‡∏Å‡∏ô X
    return;
  }

  // 2. ‡∏ï‡∏£‡∏ß‡∏à‡∏™‡∏≠‡∏ö‡∏Ñ‡∏≥‡∏™‡∏±‡πà‡∏á WATER_SPECIFIC
  // ‡∏£‡∏π‡∏õ‡πÅ‡∏ö‡∏ö‡∏Ñ‡∏≥‡∏™‡∏±‡πà‡∏á‡∏ó‡∏µ‡πà‡∏°‡∏≤‡∏à‡∏≤‡∏Å Python: "WATER_SPECIFIC:3"
  if (command.startsWith("WATER_SPECIFIC:"))
  {
    currentCommand = "WATER_SPECIFIC";
    // find symbol index from command
    int colonIndex = command.indexOf(":");
    String valueStr = command.substring(colonIndex + 1); // 3,123,456

    int firstComma = valueStr.indexOf(",");
    int SecComma = valueStr.indexOf(",", firstComma + 1);
    if (colonIndex == -1 || firstComma == -1 || SecComma == -1)
    {
      Serial.println("Invalid index value.");
      return;
    }
    // substring to assign value
    String valueStrID = valueStr.substring(0, firstComma);           // 3
    String valueStrX = valueStr.substring(firstComma + 1, SecComma); // 123
    String valueStrZ = valueStr.substring(SecComma + 1);             // 456
    // tran string type to int
    id = valueStrID.toInt();
    float pos_X = valueStrX.toFloat();
    float pos_Z = valueStrZ.toFloat();
    moveX_to(pos_X);
    moveZ_to(pos_Z);
    Serial.println(""); // ‡∏™‡πà‡∏á‡∏Å‡∏≤‡∏£‡∏ï‡∏≠‡∏ö‡∏Å‡∏•‡∏±‡∏ö
    currentState = MOVING_WATER_Z;
    return;
  }

  if (command.startsWith("CHECK_WATER_ALL")) // WATER_ALL:3,123,456
  {
    // ‡∏ñ‡πâ‡∏≤‡πÉ‡∏ä‡πâ read_all_available() ‡∏ï‡πâ‡∏≠‡∏á‡∏°‡∏µ \n ‡∏Ç‡πâ‡∏≤‡∏á‡∏´‡∏ô‡πâ‡∏≤‡πÄ‡∏û‡∏∑‡πà‡∏≠‡∏™‡πà‡∏á‡∏Ñ‡πà‡∏≤‡∏ß‡πà‡∏≤‡∏á‡∏ï‡∏≠‡∏ö‡∏Å‡∏•‡∏±‡∏ö‡∏Ñ‡∏≥‡∏™‡∏±‡πà‡∏á send_serial_command()
    Serial.println("\nWAITING_COMMAND"); // ‡∏™‡πà‡∏á‡∏Å‡∏≤‡∏£‡∏ï‡∏≠‡∏ö‡∏Å‡∏•‡∏±‡∏ö
    return;
  }

  // 3. ‡∏ï‡∏£‡∏ß‡∏à‡∏™‡∏≠‡∏ö‡∏Ñ‡∏≥‡∏™‡∏±‡πà‡∏á WATER_ALL
  if (command.startsWith("WATER_ALL:")) // WATER_ALL:3,123,456
  {
    currentCommand = "WATER_ALL";
    // find symbol index from command
    int colonIndex = command.indexOf(":");
    String valueStr = command.substring(colonIndex + 1); // 3,123,456

    int firstComma = valueStr.indexOf(",");
    int SecComma = valueStr.indexOf(",", firstComma + 1);
    if (colonIndex == -1 || firstComma == -1 || SecComma == -1)
    {
      Serial.println("Invalid index value.");
      return;
    }
    // substring to assign value
    String valueStrID = valueStr.substring(0, firstComma);           // 3
    String valueStrX = valueStr.substring(firstComma + 1, SecComma); // 123
    String valueStrZ = valueStr.substring(SecComma + 1);             // 456
    // tran string type to int
    id = valueStrID.toInt();
    float pos_X = valueStrX.toFloat();
    float pos_Z = valueStrZ.toFloat();
    moveX_to(pos_X);
    moveZ_to(pos_Z);
    Serial.println(""); // ‡∏™‡πà‡∏á‡∏Å‡∏≤‡∏£‡∏ï‡∏≠‡∏ö‡∏Å‡∏•‡∏±‡∏ö
    currentState = MOVING_WATER_Z;
    return;
  }

  // ‡∏´‡∏≤‡∏Å‡πÑ‡∏°‡πà‡∏°‡∏µ‡∏Ñ‡∏≥‡∏™‡∏±‡πà‡∏á‡πÉ‡∏î‡∏ï‡∏£‡∏á‡∏Å‡∏±‡∏ô
  Serial.print("ERROR: UNKNOWN COMMAND: ");
  Serial.println(command);
}